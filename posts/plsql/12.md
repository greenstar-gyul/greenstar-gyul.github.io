# 🟡 PL/SQL - (5)
## 프로시저(PROCEDURE)
- PL/SQL에서 자주 사용하는 SQL문이나 PL/SQL 블록을 하나의 이름 있는 단위로 묶은 것
- 재사용 가능하게 만든 코드 블록
- 독립된 기능을 구현하는 PL/SQL의 객체 중 하나
- 프로시저를 호출할 때에는 우측값으로 사용될 수 없음

- 파라미터의 모드

    | 모드     | 설명                      |
    | -------- | ------------------------- |
    | `IN`     | 값을 전달받기만 함 (기본값)          |
    | `OUT`    | 프로시저 내부에서 값을 설정해 호출자에게 전달 |
    | `IN OUT` | 값을 전달도 받고, 수정 후 되돌려줌      |

```SQL
CREATE [OR REPLACE] PROCEDURE 프로시저명(
    -- 매개변수를 선언할 때 데이터 타입의 크기는 지정하지 않음
    파라미터명1 모드(IN, OUT, IN OUT) 파라미터타입,
    ...
) 
IS
    -- 선언부 : 변수, 커서, 예외 등
BEGIN
   -- 실행문
EXCEPTION
    -- 예외처리
END;
/

-- 프로시저 실행
BEGIN
    프로시저명(파라미터 값);
END;
/
```

### IN 모드
- `매개변수명 IN 데이터타입`
- 값을 서브 프로그램에 전달
- 내부적으로 상수 취급

```SQL
CREATE PROCEDURE in_test (
    p_eid IN NUMBER
)
IS

BEGIN
    p_eid := NVL(p_eid, 100); -- 불가능!!! 컴파일 단계 에러.
                              -- BUT, 프로시저는 생성됨.
END;
/
```
```
[output]
PLS-00363: expression 'P_EID' cannot be used as an assignment target
```

- 예시
```SQL
CREATE PROCEDURE increase_salary (
    p_eid IN NUMBER
)
IS

BEGIN
    UPDATE employees
    SET salary = salary * 1.1
    WHERE employee_id = p_eid;
END;
/

DECLARE
    v_first NUMBER(3, 0) := 100;
BEGIN
    increase_salary(149);           -- 리터럴
    increase_salary(2 * v_first);   -- 표현식(계산식)
    increase_salary(v_first);       -- 값을 가진 변수
END;
/
```

### OUT 모드
- `매개변수명 IN 데이터타입`
- 값을 호출 환경으로 반환
- OUT 파라미터에 넘겨주는 변수는 값을 가지고 있어도, 프로시저 안으로 들어가는 순간 그 값은 무시되고 NULL이 됨

```sql
CREATE PROCEDURE test_p_out (
    p_num IN NUMBER,
    p_out OUT NUMBER
)
IS

BEGIN
    DBMS_OUTPUT.PUT_LINE('IN: ' || p_num);
    DBMS_OUTPUT.PUT_LINE('OUT: ' || p_out);
END;
/

DECLARE
    v_result NUMBER(4, 0) := 1234;
BEGIN
    DBMS_OUTPUT.PUT_LINE('변경 전 v_result: ' || v_result);
    test_p_out(1000, v_result); -- v_result는 1234의 값을 가지고 있지만, NULL로 초기화됨.
    DBMS_OUTPUT.PUT_LINE('변경 후 v_result: ' || v_result);
END;
/
```
```
[output]
변경 전 v_result: 
IN: 1000
IN: 
변경 후 v_result:
```

- 예시
```SQL
CREATE PROCEDURE plus(
    p_x IN NUMBER,
    p_y IN NUMBER,
    p_result OUT NUMBER
)
IS

BEGIN
    p_result := p_x + p_y;
END;
/

DECLARE
    v_total NUMBER(10, 0);
BEGIN
    plus(10, 25, v_total);
    DBMS_OUTPUT.PUT_LINE(v_total);
END;
/
```
```
[output]
35
```

### IN OUT 모드
- `매개변수명 IN 데이터타입`
- 값을 서브 프로그램에 전달하고 호출 환경으로 반환
- 주로 포맷 변환할 때 사용

```sql
CREATE PROCEDURE format_phone(
    p_phone_no IN OUT VARCHAR2
)
IS

BEGIN
    -- 1) OUT 모드와 달리 호출환경에서 전달받은 값을 가질 수 있음
    DBMS_OUTPUT.PUT_LINE('before: ' || p_phone_no);
    -- 2) IN 모드와 달리 값을 변경할 수 있음
    p_phone_no := SUBSTR(p_phone_no, 1, 3) || '-' || SUBSTR(p_phone_no, 4, 4) || '-' || SUBSTR(p_phone_no, 8);
    DBMS_OUTPUT.PUT_LINE('after: ' || p_phone_no);
END;
/

DECLARE
    v_phone_n VARCHAR2(15) := '01012345678';
BEGIN
    DBMS_OUTPUT.PUT_LINE('변경 전 v_phone_n: ' || v_phone_n);
    format_phone(v_phone_n);
    DBMS_OUTPUT.PUT_LINE('변경 후 v_phone_n: ' || v_phone_n);
END;
/
```
```
[output]
변경 전 v_phone_n: 01012345678
before: 01012345678
after: 010-1234-5678
변경 후 v_phone_n: 010-1234-5678
```