# PL/SQL - (1)
## PL/SQL
- SQL에서 프로그래밍 언어의 다양한 고급 기능(변수, 조건문, 함수 등)을 사용할 수 있도록 해주는 것
- 함수/프로시저 단위로 트랜잭션 처리 제공

```sql
SET SERVEROUTPUT ON; -- PL/SQL의 실행 결과를 표준 출력창(SQL Developer의 경우 스크립트 출력 화면)에 보이도록 설정하는 함수

BEGIN
    DBMS_OUTPUT.PUT_LINE('Hello, PL/SQL!'); -- 실행 결과를 출력창에 보이도록 해주는 것(js의 console.log(), java의 System.out.println())
END;
```
```
[output]
Hello, PL/SQL!
```

```SQL
--Anonymous Block. 그냥 한 번 실행하고 끝나는 블럭
DECLARE --선택사항
-- 선언부: 변수 등을 선언하는 영역
BEGIN --필수
-- 실행부 : 기능을 수행하는 영역
-- SQL문
-- PL/SQL문
EXCEPTION --선택사항
--오류 발생 시 수행할 작업
-- 예외처리: PL/SQL 블록을 실행할 때 발생하는 예외처리
END; --필수
/
```

## 변수
- 변수: 데이터가 저장된 메모리 공간

- 데이터 입력 검증에 사용할 데이터를 임시 저장
- 변수를 사용해 DB 액세스 없이 계산 및 데이터 조작 가능
- 선언한 변수는 선언문 또는 기타 명령문에서 참조하는 방식으로 재사용 가능
- %TYPE 및 %ROWTYPE을 사용하면 DB 열의 정의에 따라 변수 선언

### 변수 처리
- 선언 부분에서 선언 및 초기화
- 실행 부분에서 변수에 새로운 값 할당

### 변수 명명 규칙
- 변수 명과 테이블의 한 컬럼명과 동일하면 안 됨
- 30글자 이하, 시작은 반드시 문자
- v_지역변수, g_전역변수처럼 접두어를 사용해 컬럼명과의 중복 방지

### 변수 선언 방법
> <i>identifier [constant] datatype [not null] [:= | DEFAULT expr];</i><br>
변수명 [상수 여부] 자료형 [null 허용 여부] [:= 또는 DEFAULT 표현식]
```sql
-- 올바른 동작
DECLARE
    v_str VARCHAR2(100);
    v_num CONSTANT NUMBER(2,0) := 10;
    v_count NUMBER(1,0) NOT NULL DEFAULT 5;
    v_sum NUMBER(3, 0) := (v_num + v_count);
BEGIN
    v_count := 9;
    DBMS_OUTPUT.PUT_LINE('v_count : ' || v_count);
    v_str := 'Hello!!';
    DBMS_OUTPUT.PUT_LINE('v_str : ' || v_str);
    DBMS_OUTPUT.PUT_LINE('v_num : ' || v_num);
    DBMS_OUTPUT.PUT_LINE('v_sum : ' || v_sum);
END;
/
```
```
[output]
v_count : 9
v_str : Hello!!
v_num : 10
v_sum : 15
```
- 오류 1. 상수 값 변경
```sql
DECLARE
    v_str VARCHAR2(100);
    v_num CONSTANT NUMBER(2,0) := 10; -- CONSTANT로 설정 시 변수의 값 변경 불가
    v_count NUMBER(1,0) NOT NULL DEFAULT 5;
    v_sum NUMBER(3, 0) := (v_num + v_count);
BEGIN
    v_num := 100; -- 오류 발생. 
END;
/
```
```
[output]
...
PLS-00363: expression 'V_NUM' cannot be used as an assignment target
...
```
- 오류 2. NOT NULL에 NULL 할당
```sql
DECLARE
    v_str VARCHAR2(100);
    v_num CONSTANT NUMBER(2,0) := 10;
    v_count NUMBER(1,0) NOT NULL DEFAULT 5; -- NOT NULL 지정 시 NULL 할당 불가
    v_sum NUMBER(3, 0) := (v_num + v_count);
BEGIN
    v_count := null; -- NULL은 할당 불가. 
END;
/
```
```
[output]
...
PLS-00382: expression is of wrong type
...
```
- 오류 3. 데이터 타입 크기 초과
```sql
DECLARE
    v_str VARCHAR2(100);
    v_num CONSTANT NUMBER(2,0) := 10;
    v_count NUMBER(1,0) NOT NULL DEFAULT 5;
    v_sum NUMBER(3, 0) := (v_num + v_count);
BEGIN
    v_count := 9; 
    DBMS_OUTPUT.PUT_LINE(v_count);
--    v_count := null;
    v_sum := v_num + 1234; -- 데이터 자료형 표현 범위 초과
END;
/
```
```
[output]
...
ORA-06502: PL/SQL: numeric or value error: number precision too large
...
```

### 변수 유형
- 스칼라 형
  - 단일 값
  - Oracle Server 테이블의 열 유형과 일치
    - 숫자, 문자, 날짜
  - Boolean 지원

#### %TYPE 속성
- 변수를 선언할 때, 특정 변수나 테이블의 열을 참고해서 그것의 데이터 타입을 그대로 사용하는것

```sql
DECLARE
    v_comm employees.commission_pct%TYPE; -- 컬럼 이용
    v_new_comm v_comm%TYPE; -- 변수 이용
BEGIN
    DBMS_OUTPUT.PUT_LINE(v_new_comm);
END;
/
```

## 연산자
- 기본 SQL의 연산자를 모두 사용
- +, -, *, /, =, @ 등등.
- <>, !=, || 등등.
- AND, BETWEEN, LIKE 등등등...

## 함수
- SQL 함수 사용 가능
  - 단, DECODE와 그룹 함수를 사용하기 위해서는 SQL문과 함께 사용 가능

```sql
-- PL/SQL에서 함수 사용
DECLARE
    v_today DATE := SYSDATE;
    v_after_day DATE;
    v_msg VARCHAR2(100);
BEGIN
    v_after_day := ADD_MONTHS(v_today, 3);
    v_msg := TO_CHAR(v_after_day, 'yy"년" MM"월" dd"일"');
    
    DBMS_OUTPUT.PUT_LINE(v_msg);
END;
/
```

## SQL
### DDL (Data Definition Language)
- CREATE, ALTER, DROP
### DML (Data Manipulation Language)
- INSERT, UPDATE, DELETE, SELECT
### DCL (Data Control Language)
- GRANT, REVOKE
### TCL (Transaction Control Language)
- COMMIT, ROLLBACK, SAVEPOINT

### PL/SQL과 SQL
- PL/SQL 블록은 트랜잭션 단위가 아니다!
  - 블록 내에서 TCL 사용은 가능!
- PL/SQL은 DDL, DCL을 지원하지 않음!

### PL/SQL에서의 SELECT
- 기존 SELECT문을 그대로 쓰되, INTO절이 추가됨
- INTO절에는 SELECT절이 출력하는 값을 저장할 변수를 지정
- 변수는 SELECT절이 반환하는 열 만큼 선언
- 하나의 행만 반환 가능

```sql
SELECT 열1, 열2, ...
INTO   변수1, 변수2, ...
FROM   테이블명
WHERE  조건;
```
```sql
DECLARE
    v_ename employees.last_name%TYPE;
BEGIN
    SELECT last_name
    INTO   v_ename
    FROM employees
    WHERE employee_id = 100;
    
    DBMS_OUTPUT.PUT_LINE(v_ename);
END;
```